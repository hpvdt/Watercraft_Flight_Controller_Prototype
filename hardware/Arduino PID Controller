#include <Servo.h>

const int PIN_TRIG = 9;
const int PIN_ECHO = 10;
const int PIN_SERVO = 6;

const int SERVO_DEFAULT = 90;
const int SERVO_MIN = 45;
const int SERVO_MAX = 135;

const double TARGET_HEIGHT = 15.0;  // target height is to be changed -> units are in cm
const unsigned long SAMPLE_TIME = 20; // in milliseconds (ms)

struct PIDController{
    double Kp, Ki, Kd;
    double integral;
    double prev_error;
};

Servo servo;
PIDController pid;
unsigned long last_pid_time = 0;

void initialize_PID(PIDController* pid, double Kp, double Ki, double Kd){
    pid->Kp = Kp;
    pid->Ki = Ki;
    pid->Kd = Kd;
    pid->integral = 0;
    pid->prev_error = 0;
}

double read_height(){
    digitalWrite(PIN_TRIG, LOW);
    delayMicroseconds(2);
    digitalWrite(PIN_TRIG, HIGH);
    delayMicroseconds(10);
    digitalWrite(PIN_TRIG, LOW);

    long duration = pulseIn(PIN_ECHO, HIGH);

    if(duration == 0){
      return -1.0;
    }
    
    return (duration * 0.0343) / 2.0;
}

double compute_PID(PIDController* pid, double target, double current, double dt){
    double error = target - current;

    double P = pid->Kp * error;

    pid->integral += error * dt;
    if (pid->integral > 50) pid->integral = 50; // 50 is an arbituary number -> it is used to limit the "jerkiness" of the system
    if (pid->integral < -50) pid->integral = -50;
    double I = pid->Ki * pid->integral;

    double D = pid->Kd * (error - pid->prev_error) / dt;

    pid->prev_error = error;

    return P + I + D;
}

void setup(){
    Serial.begin(115200);

    pinMode(PIN_TRIG, OUTPUT);
    pinMode(PIN_ECHO, INPUT);
    servo.attach(PIN_SERVO);
    
    servo.write(SERVO_DEFAULT);

    initialize_PID(&pid, 1.5, 0.0, 0.0); // Tune the following constants

    delay(1000);
}

void loop(){
    unsigned long current_time = millis();

    if(current_time - last_pid_time >= SAMPLE_TIME){
        double dt = (current_time - last_pid_time) / 1000.0;
        last_pid_time = current_time;

        double current_height = read_height();

        if (current_height < 0 || current_height > 100){
          return;
        }
        double correction_angle = compute_PID(&pid, TARGET_HEIGHT, current_height, dt);
        double final_angle = SERVO_DEFAULT + correction_angle;
        final_angle = constrain(final_angle, SERVO_MIN, SERVO_MAX);

        servo.write(final_angle); // servo library always converts it to integers

        Serial.print("Target:"); Serial.print(TARGET_HEIGHT);
        Serial.print(" Current:"); Serial.print(current_height);
        Serial.print(" ServoAngle:"); Serial.println(final_angle);
    }
}
